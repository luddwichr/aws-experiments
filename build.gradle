buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath('io.spring.gradle:dependency-management-plugin:1.0.7.RELEASE')
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'io.spring.dependency-management'

group = 'com.example'
version = '0.0.1'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom 'com.amazonaws:aws-java-sdk-bom:1.11.564'
    }
}

ext {
    log4jVersion = '2.11.2'
}

dependencies {
    implementation 'com.amazonaws:aws-lambda-java-core:1.2.0'
    implementation 'com.amazonaws:aws-lambda-java-events:2.2.6'
    implementation 'com.amazonaws:aws-java-sdk-lambda'
    implementation 'com.amazonaws:aws-java-sdk-s3'
    implementation 'com.amazonaws:aws-lambda-java-log4j2:1.1.0'
    annotationProcessor "org.apache.logging.log4j:log4j-core:$log4jVersion"
    implementation "org.apache.logging.log4j:log4j-api:$log4jVersion"

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.assertj:assertj-core:3.12.2'
    testImplementation 'org.mockito:mockito-core:2.28.2'
    testImplementation 'cloud.localstack:localstack-utils:0.1.22'
}

task buildZip(type: Zip) {
    archiveName = 'demoLambda.zip'
    from compileJava
    from processResources
    into('lib') {
        from configurations.compileClasspath
    }
}
